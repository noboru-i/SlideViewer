apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.google.gms.oss.licenses.plugin'

apply from: "https://raw.githubusercontent.com/monstar-lab/gradle-android-ci-check/1.3.1/ci.gradle"

// CI
def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion 27
    buildToolsVersion "26.0.2"

    dataBinding {
        enabled = true
    }

    dexOptions {
        // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }

    defaultConfig {
        applicationId "hm.orz.chaos114.android.slideviewer"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 13
        versionName "3.2.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        releaseConfig {
            storeFile file("keystore")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias "chaos114"
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            zipAlignEnabled true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }
    lintOptions {
        abortOnError false
    }
}

ext {
    supportLibVersion = '27.0.2'
    playServicesVersion = '11.8.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.google.firebase:firebase-crash:${playServicesVersion}"
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.github.bumptech.glide:glide:4.5.0'
    compile "com.google.android.gms:play-services-ads:${playServicesVersion}"
    compile "com.google.android.gms:play-services-analytics:${playServicesVersion}"
    implementation "com.google.android.gms:play-services-oss-licenses:${playServicesVersion}"
    compile 'com.j256.ormlite:ormlite-android:5.0'
    compile 'com.github.chrisbanes:PhotoView:2.1.3'
    provided 'org.projectlombok:lombok:1.16.18'
    annotationProcessor 'org.projectlombok:lombok:1.16.18'
    compile "io.reactivex.rxjava2:rxjava:2.1.9"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.jakewharton.timber:timber:4.6.0'
    compile 'com.squareup.okhttp3:okhttp:3.9.1'
    compile 'org.jsoup:jsoup:1.10.3'
    compile 'com.rmtheis:tess-two:8.0.0'
    annotationProcessor 'com.rejasupotaro:kvs-schema-compiler:5.0.1'
    compile 'com.rejasupotaro:kvs-schema:5.0.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

apply plugin: 'com.google.gms.google-services'
